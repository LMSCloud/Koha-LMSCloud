#!/usr/bin/perl

# Copyright 2015 ByWater Solutions
#
# This file is part of Koha.
#
# Koha is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# Koha is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with Koha; if not, see <http://www.gnu.org/licenses>.
#

use Modern::Perl;

use CGI qw ( -utf8 );
use JSON qw(to_json);

use C4::Auth qw(check_cookie_auth);
use Koha::ArticleRequests;

my $cgi = CGI->new;

my ( $auth_status ) =
  check_cookie_auth( $cgi->cookie('CGISESSID'), { circulate => 'circulate_remaining_permissions' } );
if ( $auth_status ne "ok" ) {
    exit 0;
}

binmode STDOUT, ':encoding(UTF-8)';
print $cgi->header( -type => 'text/plain', -charset => 'UTF-8' );

my $id = $cgi->param('id');
my $action = $cgi->param('action') || q{};
my $notes = $cgi->param('notes');

my $ar = Koha::ArticleRequests->find($id);

if ($ar) {
    if ( $action eq 'cancel' ) {
        $ar = $ar->cancel({ notes => $notes });
    }
    elsif ( $action eq 'process' ) {
        $ar = $ar->process();
    }
    elsif ( $action eq 'pending' ) {
        $ar = $ar->set_pending();
    }
    elsif ( $action eq 'complete' ) {
        $ar = $ar->complete();
    }
    elsif ( $action eq 'update_branchcode' ) {
        my $branchcode = $cgi->param('branchcode');
        $ar->branchcode( $branchcode ) if $branchcode;
        $ar = $ar->store();
    }
    elsif( $action eq 'update_urls' ) {
        my $urls = $cgi->param('urls') // q{};
        $ar->urls( $urls );
        $ar = $ar->store();
    }
}

print to_json( { success => $ar ? 1 : 0 } );
