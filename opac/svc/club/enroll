#!/usr/bin/perl

# Copyright 2014 ByWater Solutions
#
# This file is part of Koha.
#
# Koha is free software; you can redistribute it and/or modify it under the
# terms of the GNU General Public License as published by the Free Software
# Foundation; either version 2 of the License, or (at your option) any later
# version.
#
# Koha is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR
# A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with Koha; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
#

use Modern::Perl;

use CGI;
use JSON qw(to_json);

use C4::Auth qw(check_cookie_auth);
use Koha::Club::Enrollment::Field;
use Koha::Club::Enrollment;
use Koha::Clubs;

my $cgi = new CGI;

my ( $auth_status, $sessionID ) =
  check_cookie_auth( $cgi->cookie('CGISESSID') );
if ( $auth_status ne "ok" ) {
    exit 0;
}

my $borrowernumber = C4::Context->userenv->{'number'};

my $id = $cgi->param('id');

my $enrollment;
if ( $borrowernumber && $id ) {
    my $club = Koha::Clubs->find($id);

    if ( $club->club_template()->is_enrollable_from_opac() ) {
        $enrollment = Koha::Club::Enrollment->new()->set(
            {
                club_id        => $club->id(),
                borrowernumber => $borrowernumber,
                date_enrolled  => \'NOW()',
                date_created   => \'NOW()',
                branchcode     => C4::Context->userenv
                ? C4::Context->userenv->{'branch'}
                : undef,
            }
        )->store();

        my @enrollment_fields = $club->club_template()->club_template_enrollment_fields();

        foreach my $e (@enrollment_fields) {
            Koha::Club::Enrollment::Field->new()->set(
                {
                    club_enrollment_id                => $enrollment->id(),
                    club_template_enrollment_field_id => $e->id(),
                    value                             => $cgi->param( $e->id() ),
                }
            )->store();
        }
    }
}

binmode STDOUT, ':encoding(UTF-8)';
print $cgi->header( -type => 'text/plain', -charset => 'UTF-8' );

print to_json( { success => $enrollment ? 1 : 0 } );
